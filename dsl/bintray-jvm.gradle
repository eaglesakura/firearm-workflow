group = artifact_group
version = artifact_version

apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'org.jetbrains.dokka'

dokka {
    outputFormat = 'javadoc'
}

task sourcesJar(type: Jar) {
    from "src/main/kotlin"
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: dokka) {
    from "$buildDir/dokka"
    classifier = 'javadoc'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

if (project.hasProperty("install_snapshot")) {
    apply plugin: 'maven'
    println("deploy to mavenLocal()")
    uploadArchives {
        repositories {
            mavenDeployer {
                pom.artifactId = artifact_name
                repository(url: "file://${System.getProperty('user.home')}/.m2/repository")
            }
        }
    }
} else {
    println("deploy to bintray.")
    bintray {
        user = bintray_user
        key = System.env.BINTRAY_API_KEY
        publish = true
        override = true
        pkg {
            repo = "maven"
            name = artifact_name
            vcsUrl = bintray_vcs_url
            issueTrackerUrl = "${bintray_vcs_url}/issues"
            licenses = ["MIT"]
            labels = bintray_labels
            version {
                name = project.version
                released = new Date()
                gpg {
                    sign = true
                    passphrase = System.env.BINTRAY_GPG_PASS
                }
            }
        }
        configurations = ['archives']
    }
}


println ":${project.name}:bintrayUpload  -> ${artifact_group} : ${version}"
println ":${project.name}:uploadArchives -> file:${System.getProperty('user.home')}/.m2/repository : ${version}"
